plugins {
    id 'java'
    id 'java-library'
    id 'io.freefair.lombok' version '8.0.0-rc2'
    id 'org.springframework.boot' version '2.7.9'
    id 'idea'
    id 'maven-publish'
    id 'com.google.cloud.tools.jib' version '3.2.0'
}

dependencies {

    // platform
    implementation platform(project(":arch4j-platform"))
    annotationProcessor platform(project(":arch4j-platform"))

    // subproject
    implementation project(":arch4j-core")

    // spring boot starter
    api 'org.springframework.boot:spring-boot-starter-batch'

    // spring batch test
    api 'org.springframework.boot:spring-boot-starter-test'
    api 'org.springframework.batch:spring-batch-test'

}

test {
    useJUnitPlatform()
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = true
    archiveClassifier = 'boot'
    mainClass = 'org.oopscraft.arch4j.batch.BatchApplication'
}

springBoot {
    buildInfo()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jib {
    System.setProperty("sendCredentialsOverHttp", "true");
    allowInsecureRegistries = true
    from {
        image = project.jibFromImage
        auth {
            username = project.jibFromAuthUsername
            password = project.jibFromAuthPassword
        }
    }
    to {
        image = "${project.jibToImageNamespace}/${project.name}"
        auth {
            username = project.jibToAuthUsername
            password = project.jibToAuthPassword
        }
        tags = [project.jibToTags]
    }
    containerizingMode = "packaged"
    container {
        format = 'OCI'
        creationTime = "USE_CURRENT_TIMESTAMP"
        ports = ['8080']
        jvmFlags = ["-Dfile.encoding=UTF-8"]
    }
}
