plugins {
    id 'java'
    id 'java-library'
    id 'io.freefair.lombok' version '8.0.0-rc2'
    id 'org.springframework.boot' version '2.7.9'
    id 'idea'
    id 'maven-publish'
    id 'application'
}

dependencies {

    // platform
    implementation platform(project(":arch4j-platform"))
    annotationProcessor platform(project(":arch4j-platform"))

    // subproject
    api project(":arch4j-core")
    api project(":arch4j-web")
    api project(":arch4j-batch")

    // picocli starter
    api 'info.picocli:picocli-spring-boot-starter'

    // springframework(for session table creation)
    api 'org.springframework.session:spring-session-jdbc'

    // spring boot starter
    api 'org.springframework.boot:spring-boot-starter-batch'

}

test {
    useJUnitPlatform()
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = true
    archiveClassifier = 'boot'
    mainClass = 'org.oopscraft.arch4j.cli.CliApplication'
}

springBoot {
    buildInfo()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

distTar {
    enabled = false
}

distZip {
    enabled = false
}

bootDistZip {
    into("${project.name}-boot-${project.version}") {
        from 'src/dist/'
    }
}

bootDistTar {
    into("${project.name}-boot-${project.version}") {
        from 'src/dist/'
    }
}
