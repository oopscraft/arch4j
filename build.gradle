allprojects {
    group = project.projectGroup
    version = project.projectVersion

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
        exclude group: 'junit', module: 'junit'
    }

    repositories {
        if(project.mavenUrl) {
            maven {
                allowInsecureProtocol = true
                url project.mavenUrl
                credentials {
                    username project.mavenUsername
                    password project.mavenPassword
                }
            }
        }
        mavenCentral()
    }

    afterEvaluate {
       // java
        if(it.plugins.hasPlugin("java")) {
            it.java {
                toolchain {
                    languageVersion = JavaLanguageVersion.of(17)
                }
                withSourcesJar()
            }
            it.compileJava.options.encoding = "UTF-8"
            it.tasks.withType(JavaCompile){
                options.encoding = 'UTF-8'
                options.compilerArgs += "-parameters"
            }
            it.tasks.withType(Jar) {
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
            it.sourceSets.main.java {
                exclude "**/*.xml"
            }
            it.sourceSets.main.resources {
                srcDirs += ["src/main/java"]
                exclude "**/*.java"
            }
            it.sourceSets.test.java {
                exclude "**/*.xml"
            }
            it.sourceSets.test.resources {
                srcDirs += ["src/test/java"]
                exclude "**/*.java"
            }
            it.java {
                withSourcesJar()
            }
        }
        // maven-publish
        if(it.plugins.hasPlugin('maven-publish')) {
            it.publishing {
                repositories {
                    maven {
                        allowInsecureProtocol = true
                        url project.publishingMavenUrl
                        credentials {
                            username project.publishingMavenUsername
                            password project.publishingMavenPassword
                        }
                    }
                }
            }
        }
    }

}
